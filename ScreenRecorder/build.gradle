buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'
    }
}
apply plugin: 'android'

dependencies {
    compile files('libs/android-support-v4.jar')
    compile files('libs/libGoogleAnalyticsV2.jar')
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
    }

    flavorDimensions "permissions", "price"

    productFlavors {
        free {
            flavorDimension "price"
        }

        pro {
            flavorDimension "price"
        }

        root {
            flavorDimension "permissions"
        }

        noRoot {
            flavorDimension "permissions"
            minSdkVersion 21
            targetSdkVersion 21
        }
    }

    // A slightly hacky way to assign app id's to flavors combinations
    // applicationIdSufix should be added to flavor
    // http://stackoverflow.com/questions/26547559/how-to-set-different-applicationid-for-each-flavor-combination-using-flavordimen/26585241#26585241
    android.variantFilter { variant ->
        def flavorString = ""
        def flavors = variant.getFlavors()
        for (int i = 0; i < flavors.size(); i++) {
            flavorString += flavors[i].name;
        }
        if(flavorString.equalsIgnoreCase("rootFree")) {
            variant.getDefaultConfig().applicationId "com.iwobanas.screenrecorder.free2"
        }
        if(flavorString.equalsIgnoreCase("rootPro")) {
            variant.getDefaultConfig().applicationId "com.iwobanas.screenrecorder.pro"
        }
        if(flavorString.equalsIgnoreCase("noRootFree")) {
            variant.getDefaultConfig().applicationId "com.iwobanas.screenrecorder.noroot.free"
        }
        if(flavorString.equalsIgnoreCase("noRootPro")) {
            variant.getDefaultConfig().applicationId "com.iwobanas.screenrecorder.noroot.pro"
        }
    }

    lintOptions {
        disable 'MissingTranslation','ExtraTranslation'
    }

    signingConfigs {

        release {
            storeFile file("../../../.android/iwo-banas-release-key.keystore")
            storePassword System.console() == null ? "IDE build" : new String(System.console().readPassword("\n\$ Enter store password: "))
            keyAlias "iwo-banas-release"
            keyPassword System.console() == null ? "IDE build" : new String(System.console().readPassword("\n\$ Enter key password: "))
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}
